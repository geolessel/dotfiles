autoload -U colors && colors
autoload -Uz vcs_info
setopt PROMPT_SUBST  # allows using command return in prompt (see vcs_info quickstart)

# I'm not going to be using any version control systems other than git, so only enable it
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true # check the repo for changes and report in prompt

# add a function to check for untracked files in the directory.
# from https://github.com/zsh-users/zsh/blob/master/Misc/vcs_info-examples
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
#
+vi-git-untracked(){
  if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
    git status --porcelain | grep '??' &> /dev/null ; then
    # This will show the marker if there are any untracked files in repo.
    # If instead you want to show the marker only if there are untracked
    # files in $PWD, use:
    #[[ -n $(git ls-files --others --exclude-standard) ]] ; then
    hook_com[staged]+='?' # signify new files with a question mark
  fi
}

zstyle ':vcs_info:git:*' formats \
       "%{$fg_bold[white]%}%{%GâŽ‡%} %{$fg_bold[magenta]%} %b%{$fg_bold[yellow]%}%m%u%c "
precmd () { vcs_info }

PROMPT=$'\n%{$fg_no_bold[cyan]%}%m:%{$fg_bold[blue]%}%3~ ${vcs_info_msg_0_}%f\n%{$fg[green]%}>%f%{$reset_color%} '
